*     WORLD DYNAMICS W5
L     P.K=P.J+(DT)(BR.JK-DR.JK)
N     P=PI
C     PI=1.65E9
R     BR.KL=(P.K)(CLIP(BRN,BRN1,SWT1,TIME.K))(BRFM.K)(BRMM.K)(BRCM.K)(BR
X     PM.K)
C     BRN=.04
C     BRN1=.04
C     SWT1=1970
A     BRMM.K=TABHL(BRMMT,MSL.K,0,5,1)
T     BRMMT=1.2/1/.85/.75/.7/.7
A     MSL.K=ECIR.K/(ECIRN)
C     ECIRN=1
A     ECIR.K=(CIR.K)(1-CIAF.K)(NREM.K)/(1-CIAFN)
A     NREM.K=TABLE(NREMT,NRFR.K,0,1,.25)
T     NREMT=0/.15/.5/.85/1
A     NRFR.K=NR.K/NRI
L     NR.K=NR.J+(DT)(-NRUR.JK)
N     NR=NRI
C     NRI=900E9
R     NRUR.KL=(P.K)(CLIP(NRUN,NRUN1,SWT2,TIME.K))(NRMM.K)
C     NRUN=1
C     NRUN1=1
C     SWT2=1970
NOTE        EQUATION 42 CONNECTS HERE FROM EQ. 4 TO EQ. 9
R     DR.KL=(P.K)(CLIP(DRN,DRN1,SWT3,TIME.K))(DRMM.K)(DRPM.K)(DRFM.K)(DR
X     CM.K)
C     DRN=.028
C     DRN1=.028
C     SWT3=1970
A     DRMM.K=TABHL(DRMMT,MSL.K,0,5,.5)
T     DRMMT=3/1.8/1/.8/.7/.6/.53/.5/.5/.5/.5
A     DRPM.K=TABLE(DRPMT,POLR.K,0,60,10)
T     DRPMT=.92/1.3/2/3.2/4.8/6.8/9.2
A     DRFM.K=TABHL(DRFMT,FR.K,0,2,.25)
T     DRFMT=30/3/2/1.4/1/.7/.6/.5/.5
A     DRCM.K=TABLE(DRCMT,CR.K,0,5,1)
T     DRCMT=.9/1/1.2/1.5/1.9/3
A     CR.K=(P.K)/(LA*PDN)
C     LA=135E6
C     PDN=26.5
A     BRCM.K=TABLE(BRCMT,CR.K,0,5,1)
T     BRCMT=1.05/1/.9/.7/.6/.55
A     BRFM.K=TABHL(BRFMT,FR.K,0,4,1)
T     BRFMT=0/1/1.6/1.9/2
A     BRPM.K=TABLE(BRPMT,POLR.K,0,60,10)
T     BRPMT=1.02/.9/.7/.4/.25/.15/.1
A     FR.K=(FPCI.K)(FCM.K)(FPM.K)(CLIP(FC,FC1,SWT7,TIME.K))/FN
C     FC=1
C     FC1=1
C     FN=1
C     SWT7=1970
A     FCM.K=TABLE(FCMT,CR.K,0,5,1)
T     FCMT=2.4/1/.6/.4/.3/.2
A     FPCI.K=TABHL(FPCIT,CIRA.K,0,6,1)
T     FPCIT=.5/1/1.4/1.7/1.9/2.05/2.2
A     CIRA.K=(CIR.K)(CIAF.K)/CIAFN
C     CIAFN=.3
A     CIR.K=CI.K/P.K
L     CI.K=CI.J+(DT)(CIG.JK-CID.JK)
N     CI=CII
C     CII=.4E9
R     CIG.KL=(P.K)(CIM.K)(CLIP(CIGN,CIGN1,SWT4,TIME.K))
C     CIGN=.05
C     CIGN1=.05
C     SWT4=1970
A     CIM.K=TABHL(CIMT,MSL.K,0,5,1)
T     CIMT=.1/1/1.8/2.4/2.8/3
R     CID.KL=(CI.K)(CLIP(CIDN,CIDN1,SWT5,TIME.K))
C     CIDN=.025
C     CIDN1=.025
C     SWT5=1970
A     FPM.K=TABLE(FPMT,POLR.K,0,60,10)
T     FPMT=1.02/.9/.65/.35/.2/.1/.05
A     POLR.K=POL.K/POLS
C     POLS=3.6E9
L     POL.K=POL.J+(DT)(POLG.JK-POLA.JK)
N     POL=POLI
C     POLI=.2E9
R     POLG.KL=(P.K)(CLIP(POLN,POLN1,SWT6,TIME.K))(POLCM.K)
C     POLN=1
C     POLN1=1
C     SWT6=1970
A     POLCM.K=TABHL(POLCMT,CIR.K,0,5,1)
T     POLCMT=.05/1/3/5.4/7.4/8
R     POLA.KL=POL.K/POLAT.K
A     POLAT.K=TABLE(POLATT,POLR.K,0,60,10)
T     POLATT=.6/2.5/5/8/11.5/15.5/20
L     CIAF.K=CIAF.J+(DT/CIAFT)(CFIFR.J*CIQR.J-CIAF.J)
N     CIAF=CIAFI
C     CIAFI=.2
C     CIAFT=15
A     CFIFR.K=TABHL(CFIFRT,FR.K,0,2,.5)
T     CFIFRT=1/.6/.3/.15/.1
A     QL.K=(QLS)(QLM.K)(QLC.K)(QLF.K)(QLP.K)
C     QLS=1
A     QLM.K=TABHL(QLMT,MSL.K,0,5,1)
T     QLMT=.2/1/1.7/2.3/2.7/2.9
A     QLC.K=TABLE(QLCT,CR.K,0,5,.5)
T     QLCT=2/1.3/1/.75/.55/.45/.38/.3/.25/.22/.2
A     QLF.K=TABHL(QLFT,FR.K,0,4,1)
T     QLFT=0/1/1.8/2.4/2.7
A     QLP.K=TABLE(QLPT,POLR.K,0,60,10)
T     QLPT=1.04/.85/.6/.3/.15/.05/.02
NOTE       EQUATION 42 LOCATED BETWEEN EQ. 4 AND 9.
A     NRMM.K=TABHL(NRMMT,MSL.K,0,10,1)
T     NRMMT=0/1/1.8/2.4/2.9/3.3/3.6/3.8/3.9/3.95/4
NOTE       INPUT FROM EQN. 38 AND 40 TO EQN. 35
A     CIQR.K=TABHL(CIQRT,QLM.K/QLF.K,0,2,0.5)
T     CIQRT=.7/.8/1/1.5/2
NOTE
NOTE       CONTROL CARDS
NOTE
C     DT=.2
C     LENGTH=2100
N     TIME=1900
A     PRTPER.K=CLIP(PRTP1,PRTP2,PRSWT,TIME.K)
C     PRTP1=0
C     PRTP2=0
C     PRSWT=0
A     PLTPER.K=CLIP(PLTP1,PLTP2,PLSWT,TIME.K)
C     PLTP1=4
C     PLTP2=4
C     PLSWT=0
PLOT  P=P(0,8E9)/POLR=2(0,40)/CI=C(0,20E9)/QL=Q(0,2)/NR=N(0,1000E9)
PLOT  FR=F,MSL=M,QLC=4,QLP=5(0,2)/CIAF=A(.2,.6)
RUN   ORIG
