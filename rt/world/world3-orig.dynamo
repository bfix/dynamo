*     WORLD3: THE DYNAMICS OF GROWTH IN A FINITE WORLD
NOTE  STANDARD MODEL
NOTE
NOTE  POPULATION SECTOR
NOTE
A     POP.K=P1.K+P2.K+P3.K+P4.K
L     P1.K=P1.J+(DT)(B.JK-D1.JK-MAT1.JK)
N     P1=P1I
C     P1I=65E7
R     D1.KL=P1.K*M1.K
A     M1.K=TABHL(M1T,LE.K,20,80,10)
T     M1T=.0567/.0366/.0243/.0155/.0082/.0023/.001
R     MAT1.KL=(P1.K)(1-M1.K)/15
L     P2.K=P2.J+(DT)(MAT1.JK-D2.JK-MAT2.JK)
N     P2=P2I
C     P2I=70E7
R     D2.KL=P2.K*M2.K
A     M2.K=TABHL(M2T,LE.K,20,80,10)
T     M2T=.0266/.0171/.0110/.0065/.0040/.0016/.0008
R     MAT2.KL=(P2.K)(1-M2.K)/30
L     P3.K=P3.J+(DT)(MAT2.JK-D3.JK-MAT3.JK)
N     P3=P3I
C     P3I=19E7
R     D3.KL=P3.K*M3.K
A     M3.K=TABHL(M3T,LE.K,20,80,10)
T     M3T=.0562/.0373/.0252/.0171/.0118/.0083/.006
R     MAT3.KL=(P3.K)(1-M3.K)/20
L     P4.K=P4.J+(DT)(MAT3.JK-D4.JK)
N     P4=P4I
C     P4I=6E7
R     D4.KL=P4.K*M4.K
A     M4.K=TABHL(M4T,LE.K,20,80,10)
T     M4T=.13/.11/.09/.07/.06/.05/.04
NOTE
NOTE  DEATH RATE SUBSECTOR
NOTE
A     D.K=D1.JK+D2.JK+D3.JK+D4.JK
S     CDR.K=1000*D.K/POP.K
A     LE.K=LEN*LMF.K*LMHS.K*LMP.K*LMC.K
C     LEN=28
A     LMF.K=TABHL(LMFT,FPC.K/SFPC,0,5,1)
T     LMFT=0/1/1.2/1.3/1.35/1.4
A     HSAPC.K=TABHL(HSAPCT,SOPC.K,0,2000,250)
T     HSAPCT=0/20/50/95/140/175/200/220/230
A     EHSPC.K=SMOOTH(HSAPC.K,HSID)
C     HSID=20
A     LMHS.K=CLIP(LMHS2.K,LMHS1.K,TIME.K,1940)
A     LMHS1.K=TABHL(LMHS1T,EHSPC.K,0,100,20)
T     LMHS1T=1/1.1/1.4/1.6/1.7/1.8
A     LMHS2.K=TABHL(LMHS2T,EHSPC.K,0,100,20)
T     LMHS2T=1/1.4/1.6/1.8/1.95/2.0
A     FPU.K=TABHL(FPUT,POP.K,0,16E9,2E9)
T     FPUT=0/.2/.4/.5/.58/.65/.72/.78/.80
A     CMI.K=TABHL(CMIT,IOPC.K,0,1600,200)
T     CMIT=.5/.05/-.1/-.08/-.02/.05/.1/.15/.2
A     LMC.K=1-(CMI.K*FPU.K)
A     LMP.K=TABHL(LMPT,PPOLX.K,0,100,10)
T     LMPT=1.0/.99/.97/.95/.90/.85/.75/.65/.55/.40/.20
NOTE
NOTE  BIRTH RATE SUBSECTOR
NOTE
R     B.KL=CLIP(D.K,(TF.K*P2.K*0.5/RLT),TIME.K,PET)
C     RLT=30
C     PET=4000
S     CBR.K=1000*B.JK/POP.K
A     TF.K=MIN(MTF.K,(MTF.K*(1-FCE.K)+DTF.K*FCE.K))
A     MTF.K=MTFN*FM.K
C     MTFN=12
A     FM.K=TABHL(FMT,LE.K,0,80,10)
T     FMT=0/.2/.4/.6/.8/.9/1/1.05/1.1
A     DTF.K=DCFS.K*CMPLE.K
A     CMPLE.K=TABHL(CMPLET,PLE.K,0,80,10)
T     CMPLET=3/2.1/1.6/1.4/1.3/1.2/1.1/1.05/1
A     PLE.K=DLINF3(LE.K,LPD)
C     LPD=20
A     DCFS.K=CLIP(2.0,DCFSN*FRSN.K*SFSN.K,TIME.K,ZPGT)
C     ZPGT=4000
C     DCFSN=4
A     SFSN.K=TABHL(SFSNT,DIOPC.K,0,800,200)
T     SFSNT=1.25/1/.9/.8/.75
A     DIOPC.K=DLINF3(IOPC.K,SAD)
C     SAD=20
A     FRSN.K=TABHL(FRSNT,FIE.K,-.2,.2,.1)
T     FRSNT=.5/.6/.7/.85/1
N     FRSN=.82
A     FIE.K=(IOPC.K-AIOPC.K)/AIOPC.K
A     AIOPC.K=SMOOTH(IOPC.K,IEAT)
C     IEAT=3
A     NFC.K=(MTF.K/DTF.K)-1
A     FCE.K=CLIP(1.0,(TABHL(FCET,FCFPC.K,0,3,.5)),TIME.K,FCEST)
C     FCEST=4000
T     FCET=.75/.85/.9/.95/.98/.99/1
A     FCFPC.K=DLINF3(FCAPC.K,HSID)
A     FCAPC.K=FSAFC.K*SOPC.K
A     FSAFC.K=TABHL(FSAFCT,NFC.K,0,10,2)
T     FSAFCT=0/.005/.015/.025/.03/.035
NOTE
NOTE  CAPITAL SECTOR
NOTE
NOTE  INDUSTRIAL SUBSECTOR
NOTE
A     IOPC.K=IO.K/POP.K
A     IO.K=(IC.K)(1-FCAOR.K)(CUF.K)/ICOR.K
A     ICOR.K=CLIP(ICOR2,ICOR1,TIME.K,PYEAR)
C     ICOR1=3
C     ICOR2=3
L     IC.K=IC.J+(DT)(ICIR.JK-ICDR.JK)
N     IC=ICI
C     ICI=2.1E11
R     ICDR.KL=IC.K/ALIC.K
A     ALIC.K=CLIP(ALIC2,ALIC1,TIME.K,PYEAR)
C     ALIC1=14
C     ALIC2=14
R     ICIR.KL=(IO.K)(FIOAI.K)
A     FIOAI.K=(1-FIOAA.K-FIOAS.K-FIOAC.K)
A     FIOAC.K=CLIP(FIOACV.K,FIOACC.K,TIME.K,IET)
C     IET=4000
A     FIOACC.K=CLIP(FIOAC2,FIOAC1,TIME.K,PYEAR)
C     FIOAC1=.43
C     FIOAC2=.43
A     FIOACV.K=TABHL(FIOACVT,IOPC.K/IOPCD,0,2,.2)
T     FIOACVT=.3/.32/.34/.36/.38/.43/.73/.77/.81/.82/.83
C     IOPCD=400
NOTE
NOTE  SERVICE SUBSECTOR
NOTE
A     ISOPC.K=CLIP(ISOPC2.K,ISOPC1.K,TIME.K,PYEAR)
A     ISOPC1.K=TABHL(ISOPC1T,IOPC.K,0,1600,200)
T     ISOPC1T=40/300/640/1000/1220/1450/1650/1800/2000
A     ISOPC2.K=TABHL(ISOPC2T,IOPC.K,0,1600,200)
T     ISOPC2T=40/300/640/1000/1220/1450/1650/1800/2000
A     FIOAS.K=CLIP(FIOAS2.K,FIOAS1.K,TIME.K,PYEAR)
A     FIOAS1.K=TABHL(FIOAS1T,SOPC.K/ISOPC.K,0,2,.5)
T     FIOAS1T=.3/.2/.1/.05/0
A     FIOAS2.K=TABHL(FIOAS2T,SOPC.K/ISOPC.K,0,2,.5)
T     FIOAS2T=.3/.2/.1/.05/0
R     SCIR.KL=(IO.K)(FIOAS.K)
L     SC.K=SC.J+(DT)(SCIR.JK-SCDR.JK)
N     SC=SCI
C     SCI=1.44E11
R     SCDR.KL=SC.K/ALSC.K
A     ALSC.K=CLIP(ALSC2,ALSC1,TIME.K,PYEAR)
C     ALSC1=20
C     ALSC2=20
A     SO.K=(SC.K*CUF.K)/(SCOR.K)
A     SOPC.K=SO.K/POP.K
A     SCOR.K=CLIP(SCOR2,SCOR1,TIME.K,PYEAR)
C     SCOR1=1
C     SCOR2=1
NOTE
NOTE  JOB SUBSECTOR
NOTE
A     J.K=PJIS.K+PJAS.K+PJSS.K
A     PJIS.K=(IC.K)(JPICU.K)
A     JPICU.K=(TABHL(JPICUT,IOPC.K,50,800,150))*1E-3
T     JPICUT=.37/.18/.12/.09/.07/.06
A     PJSS.K=(SC.K)(JPSCU.K)
A     JPSCU.K=(TABHL(JPSCUT,IOPC.K,50,800,150))*1E-3
T     JPSCUT=1.1/.6/.35/.2/.15/.15
A     PJAS.K=(JPH.K)(AL.K)
A     JPH.K=TABHL(JPHT,AIPH.K,2,30,4)
T     JPHT=2/.5/.4/.3/.27/.24/.2/.2
A     LF.K=(P2.K+P3.K)*LFPF
C     LFPF=.75
A     LUF.K=J.K/LF.K
A     LUFD.K=SMOOTH(LUF.K,LUFDT)
C     LUFDT=2
A     CUF.K=TABHL(CUFT,LUFD.K,1,11,2)
N     CUF=1
T     CUFT=1/.9/.7/.3/.1/.1
NOTE
NOTE  AGRICULTURAL SECTOR
NOTE
NOTE  LOOP 1: FOOD FROM INVESTMENT IN LAND DEVELOPMENT
NOTE
A     LFC.K=AL.K/PALT
C     PALT=3.2E9
L     AL.K=AL.J+(DT)(LDR.JK-LER.JK-LRUI.JK)
N     AL=ALI
C     ALI=.9E9
L     PAL.K=PAL.J+(DT)(-LDR.JK)
N     PAL=PALI
C     PALI=2.3E9
A     F.K=LY.K*AL.K*LFH*(1-PL)
C     LFH=.7
C     PL=.1
A     FPC.K=F.K/POP.K
A     IFPC.K=CLIP(IFPC2.K,IFPC1.K,TIME.K,PYEAR)
A     IFPC1.K=TABHL(IFPC1T,IOPC.K,0,1600,200)
T     IFPC1T=230/480/690/850/970/1070/1150/1210/1250
A     IFPC2.K=TABHL(IFPC2T,IOPC.K,0,1600,200)
T     IFPC2T=230/480/690/850/970/1070/1150/1210/1250
A     TAI.K=IO.K*FIOAA.K
A     FIOAA.K=CLIP(FIOAA2.K,FIOAA1.K,TIME.K,PYEAR)
A     FIOAA1.K=TABHL(FIOAA1T,FPC.K/IFPC.K,0,2.5,.5)
T     FIOAA1T=.4/.2/.1/.025/0/0
A     FIOAA2.K=TABHL(FIOAA2T,FPC.K/IFPC.K,0,2.5,.5)
T     FIOAA2T=.4/.2/.1/.025/0/0
R     LDR.KL=TAI.K*FIALD.K/DCPH.K
A     DCPH.K=TABHL(DCPHT,PAL.K/PALT,0,1,.1)
T     DCPHT=1E5/7400/5200/3500/2400/1500/750/300/150/75/50
NOTE
NOTE  LOOP 2: FOOD FROM INVESTMENT IN AGRICULTURAL INPUTS
NOTE
A     CAI.K=TAI.K*(1-FIALD.K)
A     AI.K=SMOOTH(CAI.K,ALAI.K)
N     AI=5E9
A     ALAI.K=CLIP(ALAI2,ALAI1,TIME.K,PYEAR)
C     ALAI1=2
C     ALAI2=2
A     AIPH.K=AI.K*(1-FALM.K)/AL.K
A     LYMC.K=TABHL(LYMCT,AIPH.K,0,1000,40)
T     LYMCT=1/3/3.8/4.4/4.9/5.4/5.7/6/6.3/6.6/6.9/7.2/7.4
X     /7.6/7.8/8/8.2/8.4/8.6/8.8/9/9.2/9.4/9.6/9.8/10
A     LY.K=LYF.K*LFERT.K*LYMC.K*LYMAP.K
A     LYF.K=CLIP(LYF2,LYF1,TIME.K,PYEAR)
C     LYF1=1
C     LYF2=1
A     LYMAP.K=CLIP(LYMAP2.K,LYMAP1.K,TIME.K,PYEAR)
A     LYMAP1.K=TABHL(LYMAP1T,IO.K/IO70,0,30,10)
T     LYMAP1T=1/1/.7/.4
A     LYMAP2.K=TABHL(LYMAP2T,IO.K/IO70,0,30,10)
T     LYMAP2T=1/1/.7/.4
C     IO70=7.9E11
NOTE
NOTE  LOOPS 1 & 2: THE INVESTMENT ALLOCATION DECISION
NOTE
A     FIALD.K=TABHL(FIALDT,(MPLD.K/MPAI.K),0,2,.25)
T     FIALDT=0/.05/.15/.30/.50/.70/.85/.95/1
A     MPLD.K=LY.K/(DCPH.K*SD)
C     SD=.07
A     MPAI.K=ALAI.K*LY.K*MLYMC.K/LYMC.K
A     MLYMC.K=TABHL(MLYMCT,AIPH.K,0,600,40)
T     MLYMCT=.075/.03/.015/.011/.009/.008/.007/.006/
X     .005/.005/.005/.005/.005/.005/.005/.005
NOTE
NOTE  LOOP 3: LAND EROSION AND URBAN-INDUSTRIAL USE
NOTE
A     ALL.K=ALLN*LLMY.K
C     ALLN=6000
A     LLMY.K=CLIP(LLMY2.K,LLMY1.K,TIME.K,PYEAR)
A     LLMY1.K=TABHL(LLMY1T,LY.K/ILF,0,9,1)
T     LLMY1T=1.2/1/.63/.36/.16/.055/.04/.025/.015/.01
A     LLMY2.K=TABHL(LLMY2T,LY.K/ILF,0,9,1)
T     LLMY2T=1.2/1/.63/.36/.16/.055/.04/.025/.015/.01
R     LER.KL=AL.K/ALL.K
A     UILPC.K=TABHL(UILPCT,IOPC.K,0,1600,200)
T     UILPCT=.005/.008/.015/.025/.04/.055/.07/.08/.09
A     UILR.K=UILPC.K*POP.K
R     LRUI.KL=MAX(0,(UILR.K-UIL.K)/UILDT)
C     UILDT=10
L     UIL.K=UIL.J+(DT)(LRUI.JK)
N     UIL=UILI
C     UILI=8.2E6
NOTE
NOTE  LOOP 4: LAND FERTILITY DEGRADATION
NOTE
L     LFERT.K=LFERT.J+(DT)(LFR.JK-LFD.JK)
N     LFERT=LFERTI
C     LFERTI=600
A     LFDR.K=TABHL(LFDRT,PPOLX.K,0,30,10)
T     LFDRT=0/.1/.3/.5
R     LFD.KL=LFERT.K*LFDR.K
NOTE
NOTE  LOOP 5: LAND FERTILITY REGENERATION
NOTE
R     LFR.KL=(ILF-LFERT.K)/LFRT.K
C     ILF=600
A     LFRT.K=TABHL(LFRTT,FALM.K,0,.10,.02)
T     LFRTT=20/13/8/4/2/2
NOTE
NOTE  LOOP 6: DISCONTINUING LAND MAINTENANCE
NOTE
A     FALM.K=TABHL(FALMT,FPR.K,0,4,1)
T     FALMT=0/.04/.07/.09/.1
A     FR.K=FPC.K/SFPC
C     SFPC=230
A     FPR.K=SMOOTH(FR.K,FSPD)
N     FPR=1
C     FSPD=2
NOTE
NOTE  NON-RENEWABLE RESOURCE SECTOR
NOTE
L     NR.K=NR.J+(DT)(-NRUR.JK)
N     NR=NRI
C     NRI=1E12
R     NRUR.KL=(POP.K)(PCRUM.K)(NRUF.K)
A     NRUF.K=CLIP(NRUF2,NRUF1,TIME.K,PYEAR)
C     NRUF1=1
C     NRUF2=1
A     PCRUM.K=TABHL(PCRUMT,IOPC.K,0,1600,200)
T     PCRUMT=0/.85/2.6/4.4/5.4/6.2/6.8/7/7
A     NRFR.K=NR.K/NRI
A     FCAOR.K=CLIP(FCAOR2.K,FCAOR1.K,TIME.K,PYEAR)
A     FCAOR1.K=TABHL(FCAOR1T,NRFR.K,0,1,.1)
T     FCAOR1T=1/.9/.7/.5/.2/.1/.05/.05/.05/.05/.05
A     FCAOR2.K=TABHL(FCAOR2T,NRFR.K,0,1,.1)
T     FCAOR2T=1/.9/.7/.5/.2/.1/.05/.05/.05/.05/.05
NOTE
NOTE  PERSISTENT POLLUTION SECTOR
NOTE
R     PPGR.KL=(PPGIO.K+PPGAO.K)*(PPGF.K)
A     PPGF.K=CLIP(PPGF2,PPGF1,TIME.K,PYEAR)
C     PPGF1=1
C     PPGF2=1
A     PPGIO.K=PCRUM.K*POP.K*FRPM*IMEF*IMTI
C     FRPM=.02
C     IMEF=.1
C     IMTI=10
A     PPGAO.K=AIPH.K*AL.K*FIPM*AMTI
C     FIPM=.001
C     AMTI=1
R     PPAPR.KL=DELAY3(PPGR.JK,PPTD)
C     PPTD=20
L     PPOL.K=PPOL.J+(DT)(PPAPR.JK-PPASR.JK)
N     PPOL=2.5E7
A     PPOLX.K=PPOL.K/PPOL70
C     PPOL70=1.36E8
A     PPASR.KL=PPOL.K/(AHL.K*1.4)
A     AHLM.K=TABHL(AHLMT,PPOLX.K,1,1001,250)
T     AHLMT=1/11/21/31/41
A     AHL.K=AHL70*AHLM.K
C     AHL70=1.5
NOTE
NOTE  SUPPLEMENTARY EQUATIONS
NOTE
S     FOA.K=(.22)(F.K)/(.22*F.K+SO.K+IO.K)
S     FOI.K=IO.K/(.22*F.K+SO.K+IO.K)
S     FOS.K=SO.K/(.22*F.K+SO.K+IO.K)
NOTE
NOTE  CONTROL CARDS FOR SIMULATION
NOTE
SPEC  DT=.5/LENGTH=2100
C     PYEAR=1975
N     TIME=1900
A     PLTPER.K=STEP(PLP,PLIT)
C     PLP=5
C     PLIT=1900
C     PRP=0
A     PRTPER.K=STEP(PRP,PRIT)+STEP(-PRP,PRTT)
C     PRIT=1900
C     PRTT=2100
PLOT  NRFR=R(0,1)/IOPC=I,FPC=F(0,1000)/POP=P(0,16E9)/PPOLX=X(0,32)
X     /CBR=B,CDR=D(0,50)
RUN   STANDARD
NOTE
NOTE  PARAMETER AND STRUCTURAL CHANGES FOR THE WORLD3 RUNS
NOTE
C     LENGTH=1970
C     PLP=2
PLOT  POP=P(0,4E9)/CBR=B,CDR=D(0,50)/LE=L(0,60)/
X     LMF=F,LMP=X,LMHS=H,LMC=C(.75,1.75)/TF=T,DTF=*(0,8)
RUN   FIGURE 7-2: POPULATION STANDARD
C     LENGTH=1970
C     PLP=2
PLOT  IC=C,IO=O(0,4E12)/IOPC=I,SOPC=S(0,400)/
X     FOA=1,FOI=2,FOS=3(0,1)
RUN   FIGURE 7-3: CAPITAL STANDARD
C     LENGTH=1970
C     PLP=2
PLOT  F=*(0,2E12)/FPC=F(0,800)/AL=A(0,1.6E9)/LY=Y(0,2000)/
X     AIPH=$(0,80)/LFERT=T(0,800)
RUN   FIGURE 7-4: AGRICULTURE STANDARD
C     LENGTH=1970
C     PLP=2
PLOT  NRUR=U(0,4E9)/NRFR=R,FCAOR=C,PCRUM=P(0,1)
RUN   FIGURE 7-5: RESOURCE STANDARD
C     LENGTH=1970
C     PLP=2
PLOT  PPGIO=I,PPGAO=A,PPGR=G,PPAPR=R(0,2E8)/PPOLX?X(0,1)/
X     AHL=L,LMP=M(0,4)
RUN   FIGURE 7-6: POLLUTION STANDARD
NOTE
NOTE  GLOBAL STANDARD RUNS
NOTE
PLOT  NRFR=R(0,1)/IOPC=I,FPC=F(0,1000)/POP=P(0,16E9)/PPOLX=X(0,32)/
X     CBR=B,CDR=D(0,50)
RUN   FIGURE 7-7: GLOBAL STANDARD
PLOT  FCAOR=C(0,1)/IO=O,TAI=A(0,4E12)/AIPH=$(0,200)/
X     FIOAA=F(0,.2)
RUN   FIGURE 7-8: GLOBAL STANDARD, B
PLOT  LY=Y(0,4000)/AL=A(0,4E9)/FPC=F(0,800)/LMF=L(0,1.6)/
X     POP=P(0,16E9)
RUN   FIGURE 7-9: GLOBAL STANDARD, C
NOTE
NOTE  SENSITIVITY TESTS
NOTE
C     NRI=2E12
PLOT  NRFR=R(0,1)/IOPC=I,FPC=F(0,1000)/POP=P(0,16E9)/PPOLX=X(0,32)/
X     CBR=B,CDR=D(0,50)
RUN   FIGURE 7-10: DOUBLE RESOURCES
C     NRI=1E13
PLOT  NRFR=R(0,1)/IOPC=I,FPC=F(0,1000)/POP=P(0,16E9)/PPOLX=X(0,32)/
X     CBR=B,CDR=D(0,50)
RUN   FIGURE 7-11: TEN TIMES RESOURCES
T     FIOAA1T=.5/.3/.1/0/0/0
T     FIOAA2T=.5/.3/.1/0/0/0
RUN   FIGURE 7-13: NEW FIOAA
C     ALIC1=21
C     ALIC2=21
RUN   FIGURE 7-14: ALIC=21 YEARS
C     ALIC1=21
C     ALIC2=21
C     ICOR1=3.75
C     ICOR2=3.75
RUN   FIGURE 7-15: ALIC=21 YEARS, ICOR=3.75 YEARS
NOTE
NOTE  TECHNOLOGICAL POLICY RUNS
NOTE
NOTE  RUNS SIMULATING DISCRETE CHANGES IN TECHNOLOGIES
NOTE
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
RUN   FIGURE 7-16: EXPLORATION TECHNOLOGIES
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
C     NRUF2=.125
RUN   FIGURE 7-18: RECYCLING TECHNOLOGIES
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
C     NRUF2=.125
T     LYMAP2T=1/1/.98/.95
RUN   FIGURE 7-19: RESOURCE, AIR POLLUTION TECHNOLOGIES
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
C     NRUF2=.125
T     LYMAP2T=1/1/.98/.95
C     PPGF2=.1
PLOT  NRFR=R(0,1)/IOPC=I(0,2000)/FPC=F(0,1000)/
X     POP=P(0,16E9)/PPOLX=X(0,32)/CBR=B,CDR=D(0,50)
RUN   FIGURE 7-20: RESOURCE, POLLUTION TECHNOLOGIES
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
C     NRUF2=.125
T     LYMAP2T=1/1/.98/.95
C     PPGF2=.1
C     LYF2=2
PLOT  NRFR=R(0,1)/IOPC=I(0,2000)/FPC=F(0,1000)/
X     POP=P(0,16E9)/PPOLX=X(0,32)/CBR=B,CDR=D(0,50)
RUN   FIGURE 7-21: RESOURCE, POLLUTION, YIELD TECHNOLOGIES
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
C     NRUF2=.125
T     LYMAP2T=1/1/.98/.95
C     PPGF2=.1
C     LYF2=2
T     LLMY2T=1.2/1/.9/.8/.75/.7/.67/.64/.62/.6
PLOT  NRFR=R(0,1)/IOPC=I(0,2000)/FPC=F(0,1000)/
X     POP=P(0,16E9)/PPOLX=X(0,32)/CBR=B,CDR=D(0,50)
RUN   FIGURE 7-22: RESOURCE, POLLUTION, AGRICULTURAL TECHNOLOGIES
NOTE
NOTE  RUN SIMULATING EXPONENTIALLY GROWING TECHNOLOGIES
NOTE
NOTE  ** THE FOLLOWING CHANGES MUST BE MADE IN EDIT MODE:
NOTE  ** IN ORDER TO MODEL EXPONENTIALLY GROWING TECHNOLOGIES...
NOTE  ** ...CHANGE:
NOTE  ** A NRUF.K=CLIP(NRUF2.K,NRUF1,TIME.K,PYEAR)
NOTE  ** A PPGF.K=CLIP(PPGF2.K,PPGF1,TIME.K,PYEAR)
NOTE  ** A LYF.K=CLIP(LYF2.K,LYF1,TIME.K,PYEAR)
NOTE  ** A NRUF2.K=EXP(-EXPON.K)
NOTE  ** A PPGF2.K=EXP(-EXPON.K)
NOTE  ** A LYF2.K=EXP(EXPON.K)
NOTE  ** A LLYM.K=CLIP(LLYM2.K,LLYM1.K,TIME.K,PYEAR)+(1-EXP(-EXPON.K))
NOTE  ** A UILT.K=UILPC.K*POP.K*EXP(-EXPON.K)
NOTE  ** ...INSERT:
NOTE  ** A EXPON.K=CLIP(ALPHA*(TIME.K-PYEAR),9,TIME.K,PYEAR)
NOTE  ** C ALPHA=.04
T     LYMAP2T=1/1/.98/.95
T     CUFT=1/1/1/1/1/1
C     DT=.1
PLOT  NRFR=R(0,1)/IOPC=I(0,2000)/POP=P(0,16E9)/PPOLX=X(0,32)/
X     CBR=B,CDR=D(0,50)
RUN   FIGURE 7-23: EXPONENTIALLY GROWING TECHNOLOGIES
NOTE
NOTE  RUNS SIMULATING ADAPTIVE CHANGES IN TECHNOLOGY
NOTE
NOTE  ** THE FOLLOWING CHANGES MUST BE MADE in EDIT MODE:
NOTE  ** IN ORDER TO MODEL RESOURCE CONTROL...
NOTE  ** ...CHANGE:
NOTE  ** A NRUF.K=CLIP(NRUF2.K,NRUF1,TIME.K,PYEAR)
NOTE  ** L NRUF2.K=NRUF2.J+(DT)(NRATE.JK)
NOTE  ** ...INSERT:
NOTE  ** N NRUF2=1
NOTE  ** R NRATE.KL=CLIP(NRUF2.K*NRCM.K,0,TIME.K,PYEAR)
NOTE  ** A NRCM.K=TABHL(NRCMT,1-NRUR.JK/DNRUR,-1,0,1)
NOTE  ** T NRCMT=-0.05/0
NOTE  ** C DNRUR=2E9
NOTE  ** IN ORDER TO MODEL YIELD CONTROL...
NOTE  ** ...CHANGE:
NOTE  ** A LYF.K=CLIP(LYF2.K,LYF1,TIME.K,PYEAR)
NOTE  ** L LYF2.K=LYF2.J+(DT)(LYF2R.JK)
NOTE  ** ...INSERT:
NOTE  ** N LYF2=1
NOTE  ** R LYF2R.KL=CLIP(LYF2.K*LYCM.K,0,TIME.K,PYEAR)
NOTE  ** A LYCM.K=TABHL(LYCMT,DFR-FR.K,0,1,1)
NOTE  ** T LYCMT=0/0.05
NOTE  ** C DFR=3
NOTE  ** IN ORDER TO MODEL POLLUTION CONTROL...
NOTE  ** ...CHANGE:
NOTE  ** A PPGF.K=CLIP(PPGF2.K,PPGF1,TIME.K,PYEAR)
NOTE  ** L PPGF2.K=PPGF2.J+(DT)(PRATE.JK)
NOTE  ** ...INSERT:
NOTE  ** N PPGF2=1
NOTE  ** R PRATE.KL=CLIP(PPGF2.K*POLGFM.K,0,TIME.K,PYEAR)
NOTE  ** A POLGFM.K=TABHL(POLGMFT,1-PPOLX.K/DPOLX,-1,0,1)
NOTE  ** T POLGMFT=-.05/0
NOTE  ** C DPOLX=3
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
T     LLMY2T=1.2/1/.9/.8/.75/.7/.67/.64/.62/.6
T     LYMAP2T=1/1/.98/.95
RUN   FIGURE 7-24: ADAPTIVE TECHNOLOGICAL POLICIES
NOTE  ** MAKE THE SAME EDIT MODE CHANGES AS IN FIGURE 7-24
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
T     LLMY2T=1.2/1/.9/.8/.75/.7/.67/.64/.62/.6
T     LYMAP2T=1/1/.98/.95
T     NRCMT=-.02/0
T     LYCMT=0/.02
T     POLGFMT=-.02/0
RUN   FIGURE 7-26: 2%/YEAR UPPER LIMIT ON TECHNOLOGICAL GROWTH
NOTE  ** MAKE THE SAME EDIT MODE CHANGES AS IN FIGURE 7-24, AND
NOTE  ** IN ORDER TO MODEL TECHNOLOGICAL COSTS...
NOTE  ** ...CHANGE:
NOTE  ** A ICOR.K=CLIP(ICOR2.K,ICOR1,TIME.K,PYEAR)
NOTE  ** A ICOR2.K=TABHL(ICOR2T,NRUF2.K,0,1,.2)*COPM.K*COYM.K
NOTE  ** ...INSERT:
NOTE  ** T ICOR2T=6/3.3/3.1/3.06/3.02/3
NOTE  ** A COPM.K=TABHL(COPMT,PPGF2.K,0,1,.2)
NOTE  ** T COPMT=2/1.1/1.05/1.02/1.01/1
NOTE  ** A COYM.K=TABHL(COYMT,LYF2.K,1,9,2)
NOTE  ** T COYMT=1/1.1/1.25/1.5/2
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
T     LLMY2T=1.2/1/.9/.8/.75/.7/.67/.64/.62/.6
T     LYMAP2T=1/1/.98/.95
RUN   FIGURE 7-27: ADAPTIVE TECHNOLOGIES WITH COSTS
NOTE  ** THE FOLLOWING CHANGES MUST BE MADE IN EDIT MODE:
NOTE  ** IN ORDER TO MODEL DELAYED RESOURCE CONTROL...
NOTE  ** ...CHANGE:
NOTE  ** A NRUF.K=CLIP(NRUF2.K,NRUF1,TIME.K,PYEAR)
NOTE  ** A NRUF2.K=DLINF3(NRTD.K,TDD)
NOTE  ** ...INSERT:
NOTE  ** C TDD=10
NOTE  ** L NRTD.K=NRTD.J+(DT)(NRATE.JK)
NOTE  ** N NRTD=1
NOTE  ** R NRATE.KL=CLIP(NRTD.K*NRCM.K,0,TIME.K,PYEAR)
NOTE  ** A NRCM.K=TABHL(NRCMT,1-NRUR.JK/DNRUR,-1,0,1)
NOTE  ** T NRCMT=-.05/0
NOTE  ** C DNRUR=2E9
NOTE  ** IN ORDER TO MODEL DELAYED YIELD CONTROL...
NOTE  ** ...CHANGE:
NOTE  ** A LYF.K=CLIP(LYF2.K,LYF1,TIME.K,PYEAR)
NOTE  ** A LYF2.K=DLINF3(LYTD.K,TDD)
NOTE  ** ...INSERT:
NOTE  ** L LYTD.K=LYTD.J+(DT)(LYTDR.JK)
NOTE  ** N LYTD=1
NOTE  ** R LYTDR.KL=CLIP(LYTD.K*LYCM.K,0,TIME.K,PYEAR)
NOTE  ** A LYCM.K=TABHL(LYCMT,DFR-FR.K,0,1,1)
NOTE  ** T LYCMT=0/.05
NOTE  ** C DFR=3
NOTE  ** IN ORDER TO MODEL DELAYED POLLUTION CONTROL...
NOTE  ** ...CHANGE:
NOTE  ** A PPGF.K=CLIP(PPGF2.K,GGPF1,TIME.K,PYEAR)
NOTE  ** A PPGF2.K=DLINF3(PTD.K,TDD)
NOTE  ** ...INSERT:
NOTE  ** L PTD.K=PTD.J+(DT)(PTDR.JK)
NOTE  ** N PTD=1
NOTE  ** R PTDR.KL=CLIP(PTD.K*POLGFM.K,0,TIME.K,PYEAR)
NOTE  ** A POLGFM.K=TABHL(POLGFMT,1-PPOLX.K/DPOLX,-1,0,1)
NOTE  ** T POLGFMT=-.05/0
NOTE  ** C DPOLX=3
NOTE  ** IN ORDER TO MODEL TECHNOLOGICAL COSTS...
NOTE  ** ...CHANGE:
NOTE  ** A ICOR.K=CLIP(ICOR2.K,ICOR1,TIME.K,PYEAR)
NOTE  ** A ICOR2.K=TABHL(ICOR2T,NRTD.K,0,1,.2)*COYM.K*COPM.K
NOTE  ** ...INSERT:
NOTE  ** T ICOR2T=6/3.3/3.1/3.06/3.02/3
NOTE  ** A COPM.K=TABHL(COPMT,PTD.K,0,1,.2)
NOTE  ** T COPMT=2/1.1/1.05/1.02/1.01/1
NOTE  ** A COYM.K=TABHL(COYMT,LYTD.K,1,9,2)
NOTE  ** T COYMT=1/1.1/1.25/1.5/2
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
T     LLMY2T=1.2/1/.9/.8/.75/.7/.67/.64/.62/.6
T     LYMAP2T=1/1/.98/.95
RUN   FIGURE 7-30: ADAPTIVE TECHNOLOGIES, COSTS, AND DELAYS
NOTE  ** MAKE THE SAME EDIT MODE CHANGES AS IN FIGURE 7-30,
NOTE  ** IN ORDER TO MODEL A BIAS TOWARDS GROWTH...
NOTE  ** ...CHANGE:
NOTE  ** A NRCM.K=TABHL(NRCMT,1-NRUR.JK/DNRUR,-1,0,1)*ICM.K
NOTE  ** A LYCM.K=TABHL(LYCMT,DFR-FR.K,0,1,1)*ICM.K
NOTE  ** A POLGFM.K=TABHL(POLGFMT,1-PPOLX.K/DPOLX,-1,0,1)*ICM.K
NOTE  ** ...INSERT:
NOTE  ** A ICM.K=TABHL(ICMT,(IOPC.K-SMOOTH(IOPC.K,2))/IOPC.K,0,.05,.05)
NOTE  ** T ICMT=0/1
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
T     LLMY2T=1.2/1/.9/.8/.75/.7/.67/.64/.62/.6
T     LYMAP2T=1/1/.98/.95
RUN   FIGURE 7-32: GROWTH BIAS
NOTE
NOTE  SOCIAL POLICY RUNS
NOTE
C     ZPGT=1975
RUN   FIGURE 7-34: DCFS=2 CHILDREN/FAMILY IN 1975
C     ALIC2=21
C     ALIC1=30
RUN   FIGURE 7-35: CAPITAL LIFETIMES
T     ISOPC2T=60/450/960/1500/1830/2175/2475/2700/3000
T     IFPC2T=345/720/1035/1275/1455/1605/1725/1815/1875
RUN   FIGURE 7-36: HIGHER FOOD, SERVICES
C     ZPGT=1975
T     ISOPC2T=60/450/960/1500/1830/2175/2475/2700/3000
T     IFPC2T=345/720/1035/1275/1455/1605/1725/1815/1875
RUN   FIGUTE 7-37: POPULATION POLICY AND OUTPUT CHOICES
NOTE
NOTE  EQUILIBRIUM POLICY RUNS
NOTE
C     ZPGT=1975
C     IET=1990
C     IOPCD=320
C     NRUF2=.125
T     ISOPC2T=60/450/960/1500/1830/2175/2475/2700/3000
T     IFPC2T=345/720/1035/1275/1455/1605/1725/1815/1875
C     PPGF2=.25
C     ALIC2=21
C     ALIC1=30
RUN   FIGURE 7-38: EQUILIBRIUM THROUGH DISCRETE POLICIES
NOTE  ** MAKE THE SAME EDIT MODE CHANGES AS IN FIGURE 7-30
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
T     LLMY2T=1.2/1/.9/.8/.75/.7/.67/.64/.62/.6
T     LYMAP2T=1/1/.98/.95
C     ZPGT=1975
C     IET=1990
C     IOPCD=320
RUN   FIGURE 7-39: EQUILIBRIUM THROUGH ADAPTIVE POLICIES
NOTE  ** MAKE THE SAME EDIT MODE CHANGES AS IN FIGURE 7-30
T     FCAOR2T=1/.2/.1/.05/.05/.05/.05/.05/.05/.05/.05
T     LLMY2T=1.2/1/.9/.8/.75/.7/.67/.64/.62/.6
T     LYMAP2T=1/1/.98/.95
C     ZPGT=2000
C     IET=2000
C     IOPCD=350
C     PYEAR=2000
RUN   FIGURE 7-41: POLICIES IN 2000

