*     PRIMER GRAN MODELO PRODUCIDO EN CHILE
L     STB.K=STB.J+DT*(R21.JK-R22.JK)
A     DFS.K=STD-STB.K
A     CPR.K=DFS.K*RCP
C     STD=20000.
C     RCP=2.0
A     CPRR.K=MIN(CPO.K,CPR.K)
R     R10.KL=DELAY1(CPRR.K,DEM1)
C     DEM1=0.1
L     CP.K=CP.J+DT*R10.JK
A     CPO.K=(CPD.K/NVP.K)-AUX3.K
A     AUX3.K=MAX(CP.K,0.0)
R     R21.KL=AUX3.K*1.14
A     DF3.K=MAX(CPR.K,0.0)-CPO.K
A     DFI.K=MAX(DF3.K,0.0)-FIN.K
L     CPD.K=CPD.J+DT*(TCD.JK-R12.JK)
R     TCD.KL=DELAY3(R32.JK,DEM2)
C     DEM2=1.0
R     R12.KL=CPD.K*0.05
L     FIN.K=FIN.J+DT*(R31.JK-R32.JK)
A     AUX1.K=DF3.K*NVP.K
R     R32.KL=MAX(AUX1.K,0.0)
R     R31.KL=MAX(DFI.K,0.0)+AHI.K
L     NVP.K=NVP.J+DT*R51.JK
A     TAP.K=AUX3.K*1.14+1.0
A     AUX4.K=DF3.K/(TAP.K*RCP)
A     AUX5.K=MAX(AUX4.K,0.0)
R     R51.KL=AUX5.K/DEM5
C     DEM5=0.25
R     R22.KL=GST.K/NVP.K
A     GST.K=YNN.K*(1-P2.K)
A     P2.K=0.16
A     AHI.K=YNN.K*P2.K
A     YNR.K=YNN.K/NVP.K
A     YNN.K=CIR.K*CTY
C     CTY=5.4
L     CIR.K=CIR.J+DT*R41.JK
R     R41.KL=MAX(DFI.K,0.0)
C     CSTB=20000.
N     STB=CSTB
C     CNVP=1.0
N     NVP=CNVP
C     CCIR=17000.
N     CIR=CCIR
C     CFIN=6000.
N     FIN=CFIN
C     CCPD=91000.
N     CPD=CCPD
C     CCP=80500.
N     CP=CCP
PRINT STB,CP,CPD,FIN,NVP,CIR
PLOT  STB=S/CP=C/CPD=P/FIN=F/NVP=N/CIR=*
SPEC  DT=0.01/LENGTH=10/PRTPER=0.1/PLTPER=0.1
RUN   PRIMERA
