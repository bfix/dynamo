*     SIMPLE EPIDEMIC MODEL
NOTE
L     SUSC.K=SUSC.J+DT*(-INF.JK)
N     SUSC=988
NOTE  SUSPECTIBLE POPULATION (PEOPLE)
R     INF.KL=SICK.K*CNTCTS.K*FRSICK
NOTE  INFECTION RATE (PEOPLE PER DAY)
C     FRSICK=0.05
NOTE  FRACTION OF CONTACTS BECOMING SICK
NOTE  (DIMENSIONLESS)
L     SICK.K=SICK.J+DT*(INF.JK-CURE.JK)
N     SICK=2
NOTE  SICK POPULATION (PEOPLE)
A     CNTCTS.K=TABLE(TABCON,SUSC.K/TOTAL,0,1,0.2)
NOTE  SUSPECTIBLE CONTACTED PER INFECTED PERSON
NOTE  PER DAY (PEOPLE PER PERSON PER DAY)
T     TABCON=0/2.8/5.5/8/9.5/10
NOTE  TABLE FOR CONTACTS
N     TOTAL=SUSC+SICK+RECOV
NOTE  TOTAL POPULATION (PEOPLE)
R     CURE.KL=SICK.K/DUR
NOTE  CURE RATE (PEOPLE PER DAY)
C     DUR=10
NOTE  DURATION OF DISEASE (DAYS)
L     RECOV.K=RECOV.J+DT*CURE.JK
N     RECOV=10
NOTE  RECOVERED POPULATION (PEOPLE)
NOTE
SPEC  DT=0.25,LENGTH=50,PRTPER=5,PLTPER=1
PRINT SUSC,SICK,RECOV,INF,CURE
PLOT  SUSC=W,SICK=S,RECOV=R(0,1000)/INF=I,CURE=C(0,200)
RUN   SIMPLE
NOTE
NOTE  **** MODIFIED MODEL WITH DELAY (INCUBATION)
NOTE
EDIT  SIMPLE
NOTE  INCUBATION DELAY (DAYS)
C     TSS=3
NOTE  FRACTION OF CONTACTS SHOWING SYMPTOMS
NOTE  (DIMENSIONLESS)
R     SYMP.KL=DELAY1(INF.JK,TSS)
L     SICK.K=SICK.J+DT*(SYMP.JK-CURE.JK)
RUN   DELAY
