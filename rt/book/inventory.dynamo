*     SIMPLE INVENTORY MODEL
NOTE
NOTE          SHIPMENTS
NOTE
R     SHIP.KL=NSHIP+TEST.K                      SHIPMENT RATE (UNITS/WK)
C        NSHIP=100
A     TEST.K=TEST1*STEP(STH,STRT)+
X         TEST2*RAMP(SLP,STRT)+
X         TEST3*PULSE(HGHT,STRT,INTVL)+
X         TEST4*AMP*SIN(6.238*TIME.K/PER)+
X         TEST5*RANGE*NOISE()
C        TEST1=0/TEST2=0/TEST3=0/TEST4=0/TEST5=0
C        STH=10,STRT=2,SLP=20,HGHT=10,INTVL=200,AMP=10,
X         PER=5,RANGE=20
L     INV.K=INV.J+DT*(ORDRCV.JK-SHIP.JK)
N        INV=DSINV                                     INVENTORY (UNITS)
R     ORDRCV.KL=DELAY3(ORDRS.JK,DEL)          ORDERS RECEIVED (UNITS/WK)
C        DEL=3 (WKS)                           DELAY IN RECEIVING ORDERS
NOTE
NOTE          ORDERS
NOTE
R     ORDRS.KL=AVSHIP.K+INVADJ.K                ORDERS PLACED (UNITS/WK)
A     AVSHIP.K=SMOOTH(SHIP.JK,TAS)      AVERAGE SHIPMENT RATE (UNITS/WK)
C        TAS=2 (WKS)                           TIME TO AVERAGE SHIPMENTS
A     INVADJ.K=(DSINV-INV.K)/IAT         INVENTORY ADJUSTMENT (UNITS/WK)
C        IAT=2 (WKS)                           INVENTORY ADJUSTMENT TIME
N     DSINV=DIC*NSHIP                          DESIRED INVENTORY (UNITS)
C        DIC=3 (WKS)                          DESIRED INVENTORY COVERAGE
NOTE
NOTE          CONTROL STATEMENTS
NOTE
SPEC  DT=.25/LENGTH=25/PRTPER=.5/PLTPER=.25
PRINT SHIP,TEST,INV,ORDRCV,ORDRS,AVSHIP,INVADJ
RUN   BASE
EDIT  BASE
C     TEST1=1
PLOT  SHIP=S(97,113)/INV=I(240,320)
RUN   STEP
EDIT  BASE
C     TEST2=1
PLOT  SHIP=S(0,800)/INV=I(0,400)
RUN   RAMP
EDIT  BASE
C     TEST3=1
PLOT  SHIP=S(97,113)/INV=I(295,304)
RUN   PULSE-1
EDIT  PULSE-1
C     INTVL=5
RUN   PULSE-2
EDIT  BASE
C     TEST4=1
PLOT  SHIP=S(80,120)/INV=I(275,315)
RUN   PERIODIC-1
EDIT  PERIODIC-1
C     PER=10
PLOT  SHIP=S(80,120)/INV=I(175,375)
RUN   PERIODIC-2
EDIT  BASE
C     TEST5=1
PLOT  SHIP=*(80,120)/INV=I(250,330)
RUN   NOISE
